import org.jetbrains.kotlin.gradle.dsl.JvmTarget

plugins {
    id 'com.android.library'
    id 'com.google.devtools.ksp'
    id 'kotlin-android'
    id 'kotlinx-serialization'
}

apply from: "$project.rootDir/flavors/flavors.gradle"

android {
    compileSdk 34
    defaultConfig {
        minSdkVersion 26
        targetSdkVersion 34
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    ksp { arg("room.schemaLocation", "$projectDir/schemas") }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_20
        targetCompatibility JavaVersion.VERSION_20
    }

    buildFeatures {
        buildConfig true
    }

    kotlin {
        compilerOptions {
            jvmTarget.set(JvmTarget.JVM_20)
        }
    }

    flavorDimensions = ["version"]

    productFlavors {
        demo {
            dimension "version"
            versionNameSuffix "-demo"
            buildConfigField "String", "BASE_URL", "\"https://api.github.com/\""
        }

        full {
            dimension "version"
            versionNameSuffix "-full"
            buildConfigField "String", "BASE_URL", "\"https://api.github.com/\""
        }
    }

    configurations {
        all*.exclude group: 'com.google.guava', module: 'listenablefuture'
    }

    packagingOptions {
        resources {
            excludes += [
                    '/META-INF/{AL2.0,LGPL2.1,LICENSE.md,LICENSE-notice.md,licenses/ASM}',
                    'win32-x86{/attach_hotspot_windows.dll,-64/attach_hotspot_windows.dll}'
            ]
        }
    }

    testOptions {
        unitTests.all { useJUnitPlatform() }
    }

    namespace 'githubprofilesearcher.caiodev.com.br.githubprofilesearcher.datasource'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation project(':modules:common:core') because 'Core Module'

    testImplementation project(':modules:common:testing') because 'JVM Testing Module'
    androidTestImplementation project(':modules:common:testing') because 'Android Testing Module'

    //DataStore
    api dep.dataStorePreferences

    //Koin
    api dep.koin

    //Logging
    debugImplementation dep.debugChucker
    releaseImplementation dep.releaseChucker

    //APIConnector
    api dep.bundles.apiConnection

    //Room
    implementation dep.bundles.room
    ksp dep.roomCompiler
}