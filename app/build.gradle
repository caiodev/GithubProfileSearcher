import org.jetbrains.kotlin.gradle.dsl.JvmTarget

plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlinx-serialization'
}

apply from: "$project.rootDir/flavors/flavors.gradle"

android {
    compileSdk 34
    defaultConfig {
        applicationId "githubprofilesearcher.caiodev.com.br.githubprofilesearcher"
        minSdkVersion 26
        targetSdkVersion 34
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            minifyEnabled false
            debuggable = true
            signingConfig signingConfigs.debug
        }

        release {
            applicationIdSuffix ".release"
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_20
        targetCompatibility JavaVersion.VERSION_20
    }

    buildFeatures {
        buildConfig true
    }

    kotlin {
        compilerOptions {
            jvmTarget.set(JvmTarget.JVM_20)
        }
    }

    packagingOptions {
        resources {
            excludes += [
                    '/META-INF/{AL2.0,LGPL2.1,LICENSE.md,LICENSE-notice.md,licenses/ASM}',
                    'win32-x86{/attach_hotspot_windows.dll,-64/attach_hotspot_windows.dll}'
            ]
        }
    }

    testOptions {
        unitTests.all { useJUnitPlatform() }
    }

    namespace 'githubprofilesearcher.caiodev.com.br.githubprofilesearcher'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation project(':modules:common:core') because 'Core Module'
    implementation project(':modules:common:datasource') because 'Datasource Module'
    implementation project(':modules:common:ui') because 'UI Module'
    implementation project(':modules:features:profile') because 'Profile Module'

    testImplementation project(':modules:common:testing') because 'JVM Testing Module'
    androidTestImplementation project(':modules:common:testing') because 'Android Testing Module'

    debugImplementation dep.leakCanary
}